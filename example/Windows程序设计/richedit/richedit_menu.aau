//响应通知,添加右键菜单
import win.ui;
/*DSG{{*/
var winform = ..win.form(text="richedit 弹出菜单";right=432;bottom=279;parent=...)
winform.add(
btnSetFont={cls="button";text="改变选区字体";left=240;top=236;right=343;bottom=263;db=1;dr=1;z=2};
richedit={cls="richedit";text="richedit 默认没有右键菜单,
使用 winform.richedit.popMenu() 函数创建弹出菜单.
请点击这里点击右键测试.";left=28;top=28;right=417;bottom=231;db=1;dl=1;dr=1;dt=1;edge=1;multiline=1;z=1}
)
/*}}*/

winform.btnSetFont.oncommand = function(id,event){ 
	/*
	关于 Text Object Model 请参考MSDN
	http://msdn.microsoft.com/en-us/library/windows/desktop/bb787607%28v=vs.85%29.aspx
	*/
	var textDoc = winform.richedit.getTextObjectModel()
	textDoc.Selection.Font.Name = "隶书";
	textDoc.Selection.Font.Bold = textDoc.tomTrue 
	textDoc.Selection.Font.Size = 18;
	textDoc.Selection.Font.ForeColor = gdi.RGB(0xff,0xA0,0);
	
	/*
	io.open()
	com.DumpTypeInfo( textDoc.Selection.Font ) //列出COM对象属性、方法
	*/
}

//如果需要接收_EN_CHANGE通知就要加下面这句
winform.richedit.modifyEvent(,0x1/*_ENM_CHANGE*/)
winform.richedit.oncommand = function(id,event){
    if(event == 0x300/*_EN_CHANGE*/){ 
        winform.text = "文本发生改变了"
    } 
}

import win.ui.menu;//必须导入菜单支持库
winform.richedit.wndproc = function(hwnd,message,wParam,lParam){
	if( message == 0x205/*_WM_RBUTTONUP*/  ){ 
	   winform.richedit.popMenu();
	}
}
 
winform.show() 
win.loopMessage();
