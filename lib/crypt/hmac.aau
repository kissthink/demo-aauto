//hmac 算法
import crypt;
namespace crypt

class hmac{
	ctor(key,data,algId = 0x8004/*_CALG_SHA1*/) {
		var csp = ..crypt(); 
		
		var key_blob = {
			struct hdr ={
				BYTE bType = 8/*_PLAINTEXTKEYBLOB*/;
				BYTE bVersion = 2/*_CUR_BLOB_VERSION*/;
				WORD reserved;
				INT aiKeyAlg = 0x6602/*_CALG_RC2*/; //RC2 is not really used
			};
			INT len = #key;
			BYTE key[] = key;
		};
	   	var ret,err = csp.importKey(key_blob, 0x100/*_CRYPT_IPSEC_HMAC_KEY*/ );
		if(!ret) return null,err;
		 
		var hashMac,err = csp.createHashByKey(0x8009/*_CALG_HMAC*/); 
		if( !hashMac ) return null,err; 
		
		var hmacInfo = HMAC_INFO();
		hmacInfo.hashAlgid = algId; 
		hashMac.setParamStruct( 5/*_HP_HMAC_INFO*/, hmacInfo );
		
		if( data ){
			hashMac.hashData(data);
		} 
		
		this = hashMac;
	} 
}

namespace hmac{
	sha1 = function(key,data){
		return ..crypt.hmac(key,data,0x8004/*_CALG_SHA1*/);
	}
	md5 = function(key,data){
		return ..crypt.hmac(key,data,0x8003/*_CALG_MD5*/);
	}
	
	class HMAC_INFO {
    	INT hashAlgid;
    	pointer pbInnerString;
    	INT cbInnerString;
    	pointer pbOuterString;
    	INT cbOuterString;
	};
}

/**intellisense()
crypt.hmac(.("密钥","源数据",算法ID) = 返回HMAC哈希对象,算法ID默认为 _CALG_SHA1
crypt.hmac.sha1(.("密钥","源数据") = 返回 HMAC-SHA1 哈希对象
crypt.hmac.md5(.("密钥","源数据") = 返回 HMAC-MD5 哈希对象
crypt.hmac() = !crypt_hash.
crypt.hmac.sha1() = !crypt_hash.
crypt.hmac.md5() = !crypt_hash.
end intellisense**/