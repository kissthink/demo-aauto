//stream 资源文件流
namespace string;

class stream{
	ctor( path ){
		if(  ..io.exist(path) ){
			this.handle = ..io.open(path,"rb");
			this.isfile = true;
		}
		else {
			this.handle = path;
			if( #path < 0x104/*_MAX_PATH*/ ){
				var s = ..string.load(path);
				if( s ) this.handle = s;
			}
			this.pos = 1;
		}
	}; 
	close = function(){
		if(!this.handle) return;
		if( this.isfile  ) this.handle.close();
		this.handle = null;
	};
	size = function(){
		if( ! this.isfile ) return #this.handle;
		else return this.handle.size(1)  	
	};
	read =  function(size){ 
		var s;
		if( ! this.isfile ){
			s = ..string.sub(this.handle,this.pos,this.pos + size);
			this.pos = this.pos + #s;
		}
		else {
			s = this.handle.read(size)
		} 
		return s;
	};
	readMemory =  function(buffer,size){ 
		if(!size) error("请指定内存长度",2);
		var s;
		if( ! this.isfile ){
			s = ..string.sub(this.handle,this.pos,this.pos + size);
			this.pos = this.pos + #s;
		}
		else {
			s = this.handle.read(size)
		}
		::CopyMemory(buffer,s,#s);
		return #s;
	};
	seek = function(offset,origin){
		if(!origin) origin="set"; 
		if( this.isfile ){
			return this.handle.seek( origin,offset);
		} 
		
		var pos;
		if( origin == "set" )
			pos = offset;
		elseif( origin = "cur" )
			pos = this.pos + offset;
		else 
			pos = #this.handle + offset;
			
		if( pos > #this.handle ) return -1;
		this.pos = pos;
		return 0;
	};
}

/**intellisense()
string.stream(.(文件路径) = 支持资源文件,\n参数也可以是普通字符串内容,\n创建的流是只读的
string.stream() = !stdStringStream.
!stdStringStream.read(.(长度) = 读取内容,返回字符串
!stdStringStream.readMemory(.(内存指针,长度) = 读取内容到内存,返回读取成功字节数
!stdStringStream.seek(.(偏移数值,"set") = 移动流指针,\n参数@2为以下值:\n"set":相对文件开始:\n"end":相对文件尾:\n"cur":相对当前位置
end intellisense**/
